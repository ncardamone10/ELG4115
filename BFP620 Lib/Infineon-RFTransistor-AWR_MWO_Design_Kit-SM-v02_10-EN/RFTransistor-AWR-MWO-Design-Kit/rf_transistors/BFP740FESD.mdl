
***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	10	GHZ
*	>>>	BFP740FESD	<<<
*	(C)	2011	Infineon	Technologies	AG
*	Version 2.1	November	2011
***************************************************************
* - Please use the global SPICE GP parameter TEMP to specify the
*	junction temperature of the device in your application to get
*	correct simulation results. This procedure is necessary because
*	the GP model does not consider the self heating of the device.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated DC power Pdc.
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 315 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature during measurement
*   for the parameter extraction. Please do not change this value.
****************************************************************
*.OPTION TNOM=25, GMIN= 1.00e-12
*BFP740FESD C B E1 E2
.SUBCKT BFP740FESD 1 2 3 4
*
*
CBEPAR 22 33 2.238E-013
CBCPAR 22 11 3.747E-014
CCEPAR 11 33 1.739E-014
LB    22 20 6.808E-010
LE   33 30 5.756E-011
LC   11 10  3.687E-010
CBEPCK 20 30  2.474E-014
CBCPCK 20 10  1.304E-015
CCEPCK 10 30  1.148E-014
LBX    20 2 5.452E-015
LEX   30 35 4.806E-011
LCX   10 1  3.738E-011
*
*
*
D1 33 25 M_D1
D2 55 25  M_D2
RBLfdb 22 25 3.2
RPS 33 55 0.5524
RSUB 30 55 0.3417
RE1 35 3 1E-03
RE2 35 4 1E-03
*
D3 55 15 M_D3
D4 23 33 M_D4
D5 23 15 M_D5
RLDNBL 15 11 14.8
*
*
Q1 11 22 33 55 M_BFP740FESD
*
*
.MODEL M_D1 D(
+ IS=3.5E-015
+ N=1.02
+ RS=6.1
+ CJO=4E-014)
*
.MODEL M_D2 D(
+ IS=800E-018
+ N=1
+ RS=4170
+ CJO=2.5E-014)
*
*
.MODEL M_D3 D(
+ IS=800E-018
+ N=1.02
+ RS=1380
+ CJO =3E-014)
*
*
.MODEL M_D4 D(
+ IS=3.5E-015
+ N=1.02
+ RS=0.2
+ CJO =3E-014)
*
.MODEL M_D5 D(
+ IS=3.5E-015
+ N=1.02
+ RS=4.7
+ CJO =3E-014)
*
*
*
*
.MODEL 	M_BFP740FESD	NPN(
+	TNOM = 25
+	IS	=	1.136E-015
+	BF	=	738.2
+	NF	=	1.014
+	VAF	=	355.5
+	IKF	=	0.1325
+	ISE	=	5.97E-015
+	NE	=	1.737
+	BR	=	89.78
+	NR	=	0.98
+	VAR	=	1.396
+	IKR	=	0.1924
+	ISC	=	3.85E-015
+	NC	=	1.5
+	RB	=	3.631
+	IRB	=	0
+	RBM	=	2.02
+	RE	=	0.05983
+	RC	=	6.855
+	XTB	=	-2.154
+	EG	=	1.11
+	XTI	=	2.728
+	CJE	=	1.08E-013
+	VJE	=	0.5522
+	MJE	=	0.2709
+	TF	=	1.596E-012
+	XTF	=	7.3788
+	VTF	=	2.70313
+	ITF	=	0.712422
+	PTF	=	0.2896
+	CJC	=	7.551E-014
+	VJC	=	0.9845
+	MJC	=	0.4393
+	XCJC	=	0.6466
+	TR	=	2.3E-007
+	CJS	=	6.09E-013
+	MJS	=	0.1942
+   VJS =   0.7431
+	FC	=	0.8156
+	KF	=	732E-12
+	AF	=	1.948)
***************************************************************
*
*
.ENDS BFP740FESD
