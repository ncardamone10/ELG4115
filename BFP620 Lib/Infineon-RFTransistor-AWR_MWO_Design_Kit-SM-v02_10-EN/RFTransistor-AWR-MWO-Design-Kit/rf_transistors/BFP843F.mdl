***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	12	GHZ
*	>>>	BFP843F	<<<
*	(C)	2013	Infineon	Technologies	AG
*	Version 1.1	MAR	2013
***************************************************************
* - Please use the global SPICE GP parameter TEMP to specify the
*	junction temperature of the device in your application to get
*	correct simulation results. This procedure is necessary because
*	the GP model does not consider the self heating of the device.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated DC power Pdc.
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 395 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature during measurement
*   for the parameter extraction. Please do not change this value.
****************************************************************
*.OPTION TNOM=25, GMIN= 1.00e-12
*BFP843F C B E1 E2
.SUBCKT BFP843F 1 2 3 4
*
RE1 35 3 1E-03
RE2 35 4 1E-03
CBEPAR 22 33 2.709E-013
CBCPAR 22 11 4.093E-014
CCEPAR 11 33 3.393E-013
*
Cfeed 11 66 5.2E-012
Rfeed 66 22 1000
Cfeedpar 66 33 2.662E-013
*
LB    22 20 5.524E-010
LE   33 30 1.501E-010
LC   11 10  4.66E-010
CBEPCK 20 30  3E-014
CBCPCK 20 10  1E-015
CCEPCK 10 30  3E-014
LBX    20 2 1.343E-010
LEX   30 35 7.069E-011
LCX   10 1  1.444E-010
*
K_BC  LB LC 0.001
K_EB  LE LB 0.1508
K_EC  LE LC 0.1017
*
R_CS_npn 55 5 500
*
D1 33 25 M_D1
D2 5 25  M_D2
*
R_NBL_fdb 22 25 3.2
R_PS 33 5 0.2
RSUB 30 5 0.3859
*
D3 5 15 M_D3
D4 23 33 M_D4
D5 23 15 M_D5
*
R_NBL_e11g 15 11 1.8
*
Q1 11 22 33 55 M_BFP843F
*
.MODEL M_D1 D(
+ IS=3E-015
+ N=1
+ RS=2.846
+ CJO=4E-014)
*
.MODEL M_D2 D(
+ IS=3E-015
+ N=1
+ RS=4700
+ CJO=4.5E-014)
*
.MODEL M_D3 D(
+ IS=6.911E-016
+ N=1.1
+ RS=1380
+ CJO =9.5E-014)
*
.MODEL M_D4 D(
+ IS=3.5E-015
+ N=1
+ RS=0.2
+ CJO =3E-014)
*
.MODEL M_D5 D(
+ IS=3.5E-015
+ N=1.02
+ RS=4.7
+ CJO =3E-014)
*
.MODEL 	M_BFP843F	NPN(
+	TNOM = 25
+	IS	=	2.226E-016
+	BF	=	850.3
+	NF	=	0.997
+	VAF	=	375.1
+	IKF	=	0.1713
+	ISE	=	5.015E-015
+	NE	=	1.958
+	BR	=	98.98
+	NR	=	0.9941
+	VAR	=	1.375
+	IKR	=	0.05974
+	ISC	=	1.578E-015
+	NC	=	1.8
+	RB	=	6.217
+	IRB	=	0
+	RBM	=	1.774
+	RE	=	0.3424
+	RC	=	7.24
+	XTB	=	-2.276
+	EG	=	1.11
+	XTI	=	0
+	CJE	=	8.091E-014
+	VJE	=	0.9141
+	MJE	=	0.06778
+	TF	=	8.467E-013
+	XTF	=	1
+	VTF	=	0.1832
+	ITF	=	0.5237
+	PTF	=	0.5
+	CJC	=	3.125E-014
+	VJC	=	0.6892
+	MJC	=	0.5349
+	XCJC	=	0.8
+	TR	=	1E-009
+	CJS	=	2.575E-013
+	MJS	=	0.5599
+   VJS =   0.4343
+	FC	=	0.5715
+	KF	=	2.476E-016
+	AF	=	1.517)
***************************************************************
*
.ENDS BFP843F
