***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	10	GHZ
*	>>>	BFR340F	<<<
*	(C)	2012	Infineon	Technologies	AG
*	Version 2.1	January	2012

***************************************************************
* - Please use the global SPICE GP parameter TEMP to specify the
*	junction temperature of the device in your application to get
*	correct simulation results. This procedure is necessary because
*	the GP model does not consider the self heating of the device.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated DC power Pdc.
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 530 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature during measurement
*   for the parameter extraction. Please do not change this value.
****************************************************************

*.OPTION TNOM=25, GMIN= 1.00e-12
*BFR340F C B E
.SUBCKT BFR340F 1 2 3
*
CBEPAR 22 33 3.138E-014
CBCPAR 22 11 5.04E-014
CCEPAR 11 33 1.229E-013
LB    22 20 5.086E-010
LE   33 30 4.152E-010
LBx    20 2 4.824E-011
LEx   30 3 5.149E-011
LCx   11 1  3.501E-010
CBEPCK 20 30  4.195E-015
CBCPCK 20 11  4.235E-014
CCEPCK 11 30  2.35E-014
*
Q1 11 22 33 M_BFR340F
*
.MODEL 	M_BFR340F	NPN(
+	TNOM = 25
+	IS	=	2.588E-017
+	BF	=	208.3
+	NF	=	1
+	VAF	=	80.12
+	IKF	=	0.1012
+	ISE	=	1.921E-015
+	NE	=	2
+	BR	=	8.99
+	NR	=	0.95
+	VAR	=	2.277
+	IKR	=	0.1924
+	ISC	=	3.85E-015
+	NC	=	1.5
+	RB	=	10.26
+	IRB	=	0
+	RBM	=	4.697
+	RE	=	1.016
+	RC	=	5.655
+	XTB	=	-1.003
+	EG	=	1.11
+	XTI	=	2.728
+	CJE	=	9.724E-014
+	VJE	=	0.693
+	MJE	=	0.2059
+	TF	=	6.027E-012
+	XTF	=	12.98
+	VTF	=	1.928
+	ITF	=	0.04152
+	PTF	=	0.1
+	CJC	=	1.25E-013
+	VJC	=	0.5602
+	MJC	=	0.09189
+	XCJC	=	0.7499
+	TR	=	8.853E-005
+	CJS	=	0
+	MJS	=	1
+	VJS	=	1
+	FC	=	0.5
+	KF	=	3.4E-10
+	AF	=	2.1)
***************************************************************
*
*
.ENDS BFR340F
