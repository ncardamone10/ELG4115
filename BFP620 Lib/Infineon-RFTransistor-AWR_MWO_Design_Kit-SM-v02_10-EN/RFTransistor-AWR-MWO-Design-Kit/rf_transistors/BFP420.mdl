
***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	10	GHZ
*	>>>	BFP420	<<<
*	(C)	2012	Infineon	Technologies	AG
*	Version 2.1	January	2012
***************************************************************
* - Please use the global SPICE GP parameter TEMP to specify the
*	junction temperature of the device in your application to get
*	correct simulation results. This procedure is necessary because
*	the GP model does not consider the self heating of the device.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated DC power Pdc.
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 260 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature during measurement
*   for the parameter extraction. Please do not change this value.
****************************************************************
*.OPTION TNOM=25, GMIN= 1.00e-12
*BFP420 C B E1 E2
.SUBCKT BFP420 1 2 3 4
*
CBEPAR 22 33 5.592E-014
CBCPAR 22 11 1.143E-013
CCEPAR 11 33 1.596E-013
LB    22 20 7.42E-010
LE   33 30 1.656E-010
LC   11 10  7.7E-010
CBEPCK 20 30  4.741E-014
CBCPCK 20 10  1.135E-015
CCEPCK 10 30  4.48E-014
LBX    20 2 2.339E-010
LEX   30 35 4.189E-011
LCX   10 1  2.243E-010
*
RE1 35 3 1E-03
RE2 35 4 1E-03
RSub 55 33 176.4
*
Diode_sub 55 11 M_Diode_sub
*
Q1 11 22 33 55 M_BFP420
*
.MODEL M_Diode_sub D(
+	IS	=	4.356E-015
+	N	=	1.02
+	RS	=	0.1
+	CJO	=	4E-014)
*
.MODEL 	M_BFP420  	NPN(
+	TNOM = 25
+	IS	=	3.753E-017
+	BF	=	140.8
+	NF	=	0.9996
+	VAF	=	59.18
+	IKF	=	0.6604
+	ISE	=	1E-014
+	NE	=	2
+	BR	=	13.61
+	NR	=	0.9938
+	VAR	=	2.685
+	IKR	=	0.007013
+	ISC	=	3.49E-016
+	NC	=	1.5
+	RB	=	11.99
+	IRB	=	0
+	RBM	=	1.521
+	RE	=	0.3062
+	RC	=	2.152
+	XTB	=	0.1187
+	EG	=	1.11
+	XTI	=	4.162
+	CJE	=	5.621E-013
+	VJE	=	0.4892
+	MJE	=	0.2565
+	TF	=	4.83855E-012
+	XTF	=	6.245
+	VTF	=	10.66
+	ITF	=	0.5436
+	PTF	=	0
+	CJC	=	1.227E-013
+	VJC	=	0.8453
+	MJC	=	0.6803
+	XCJC	=	0.7
+	TR	=	4.7E-009
+	CJS	=	4.757E-013
+	MJS	=	1
+	VJS	=	0.5323
+	FC	=	0.4148
+	KF	=	65.9E-12
+	AF	=	2.0)
***************************************************************
*
.ENDS BFP420
