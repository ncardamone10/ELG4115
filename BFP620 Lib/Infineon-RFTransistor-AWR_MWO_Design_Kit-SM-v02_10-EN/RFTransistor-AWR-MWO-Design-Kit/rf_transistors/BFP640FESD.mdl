
***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	10	GHZ
*	>>>	BFP640FESD	<<<
*	(C)	2010	Infineon	Technologies	AG
*	Version 1.1	April	2016
***************************************************************
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC
*   simulation results.
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated power
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage).
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 300 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************
*.OPTION TNOM=25, GMIN= 1.00e-12
*BFP640FESD C B E1 E2
.SUBCKT BFP640FESD 1 2 31 32
*
*
CBEPAR 22 33 3.13946E-013
CBCPAR 22 11 2.787E-014
CCEPAR 11 33 3.39308E-015
LB    22 20 5.04561E-010
LE   33 30 9.88791E-011
LC   11 10  5.104E-010
CBEPCK 20 30  5.49758E-014
CBCPCK 20 10  8.469E-015
CCEPCK 10 30  1.00975E-014
LBX    20 2 4.005E-011
LEX   30 3 4.176E-011
LCX   10 1  2.07E-011
*
*
D1 33 25 M_D1
D2 26 25  M_D2
RBLfdb 22 25 3.2
RPWf 33 26 7.64
RPWe 33 16 11.27
RSf  26  4  477
RSe  16  4  350
RCs  44  4  0.26
RSUB 30 4 1.00e-12
D3 16 15 M_D3
D4 23 33 M_D4
D5 23 15 M_D5
RLDNBL 15 11 14.8
*
RE1 3 31 1E-03
RE2 3 32 1E-03
*
*
Q1 11 22 33 44 M_BFP640FESD
*
*
.MODEL M_D1 D(
+ IS=3.5E-015
+ N=1.02
+ RS=6.1
+ CJO=2E-014)
*
*
.MODEL M_D2 D(
+ IS=3.5E-015
+ N=1.02
+ RS=0
+ CJO=2.25E-014)
*
*
.MODEL M_D3 D(
+ IS=3.5E-015
+ N=1.02
+ RS=0
+ CJO =9.57E-014)
*
*
.MODEL M_D4 D(
+ IS=3.5E-015
+ N=1.02
+ RS=0.2
+ CJO =3E-014)
*
.MODEL M_D5 D(
+ IS=3.5E-015
+ N=1.02
+ RS=4.7
+ CJO =3E-014)
*
*
*
*
.MODEL 	M_BFP640FESD	NPN(
+	TNOM = 25
+	IS	=	1.542E-015
+	BF	=	642.6
+	NF	=	1.014
+	VAF	=	355.5
+	IKF	=	0.1782
+	ISE	=	3.98E-015
+	NE	=	1.737
+	BR	=	49.18
+	NR	=	0.98
+	VAR	=	1.378
+	IKR	=	0.1924
+	ISC	=	3.85E-015
+	NC	=	1.5
+	RB	=	6.00965
+	IRB	=	9.099E-006
+	RBM	=	1.74736
+	RE	=	0.0142
+	RC	=	4.54
+	XTB	=	-2.514
+	EG	=	1.11
+	XTI	=	0.808
+	CJE	=	1.676E-013
+	VJE	=	0.6804
+	MJE	=	0.2508
+	TF	=	1.836E-012
+	XTF	=	2.279
+	VTF	=	0.9972
+	ITF	=	0.6365
+	PTF	=	0.2896
+	CJC	=	8.39234E-014
+	VJC	=	0.5464
+	MJC	=	0.3098
+	XCJC	=	0.6466
+	TR	=	1.489E-007
+	CJS	=	2.15519E-013
+	MJS	=	0.2426
+   VJS =   0.29
+	FC	=	0.8156
+	KF	=	123.5E-12
+	AF	=	1.89)
***************************************************************
*
*
.ENDS BFP640FESD
