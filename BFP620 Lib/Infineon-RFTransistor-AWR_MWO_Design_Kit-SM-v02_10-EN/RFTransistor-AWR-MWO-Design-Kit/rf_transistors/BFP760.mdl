
***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	10	GHZ
*	>>>	BFP760	<<<
*	(C)	2012	Infineon	Technologies	AG
*	Version	1.1	November	2012
***************************************************************
* - Please use the global SPICE GP parameter TEMP to specify the
*	junction temperature of the device in your application to get
*	correct simulation results. This procedure is necessary because
*	the GP model does not consider the self heating of the device.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated DC power Pdc.
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 230 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature during measurement
*   for the parameter extraction. Please do not change this value.
****************************************************************
.OPTION TNOM=25, GMIN= 1.00e-12
*BFP760 C B E1 E2
.SUBCKT BFP760 1 2 3 4
*
*
CBEPAR 22 33 1.054E-013
CBCPAR 22 11 3.5E-014
CCEPAR 11 33 2.4E-013
LE    33 30 1.093E-010
LB    22 20 5.084E-010
LC   11 10  5.3E-010
CBEPCK 20 30  4E-014
CBCPCK 20 10  1E-015
CCEPCK 10 30  4E-014
LBX    20 2 2.4E-010
LEX   30 35 7.649E-011
LCX   10 1  2.4E-010
RSUB 30 5 0.19
RSUBNPN 55 5 213.3
Rpsinker 5 33 0.16
*
RE1 35 3 1E-03
RE2 35 4 1E-03
*
Q1 11 22 33 55 M_BFP760
*
.MODEL 	M_BFP760	NPN(
+	TNOM = 25
+	IS	=	1.431E-015
+	BF	=	643.3
+	NF	=	1
+	VAF	=	482.4
+	IKF	=	0.1283
+	ISE	=	6.142E-015
+	NE	=	1.671
+	BR	=	132.5
+	NR	=	1
+	VAR	=	1.898
+	IKR	=	0.04884
+	ISC	=	3.034E-015
+	NC	=	2
+	RB	=	5
+	IRB	=	0
+	RBM	=	1.33
+	RE	=	0.1844
+	RC	=	4.861
+	XTB	=	-2.428
+	EG	=	1.104
+	XTI	=	0
+	CJE	=	5.559E-013
+	VJE	=	1
+	MJE	=	0.04358
+	TF	=	1.951E-012
+	XTF	=	35.21
+	VTF	=	1.518
+	ITF	=	4
+	PTF	=	0.1
+	CJC	=	1.618E-013
+	VJC	=	0.4
+	MJC	=	0.2341
+	XCJC	=	0.7
+	TR	=	1.6E-009
+	CJS	=	2.721E-013
+	MJS	=	0.2237
+   VJS =   0.5
+	FC	=	0.3851
+	KF	=	6.44E-017
+	AF	=	1.6)
***************************************************************
*
*
.ENDS BFP760
