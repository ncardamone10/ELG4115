
***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	10	GHZ
*	>>>	BFP740F	<<<
*	(C)	2012	Infineon	Technologies	AG
*	Version	1.1	November	2012
***************************************************************
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC
*   simulation results.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated power
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage).
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 300 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note -Thermal Resistance Calculation- AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
***************************************************************

*.OPTION TNOM=25, GMIN= 1.00e-12
*BFP740F C B E1 E2
.SUBCKT BFP740F 1 2 3 4
*
CBEPAR 22 33 1.528E-013
CBCPAR 22 11 6.274E-014
CCEPAR 11 33 1.958E-013
LE    33 30 1.588E-010
LB    22 20 3.61986E-010
LC   11 10  3.38494E-010
CBEPCK 20 30  3.5E-014
CBCPCK 20 10  1E-015
CCEPCK 10 30  3.5E-014
LBX    20 2 1.78E-010
LEX   30 35 5.40267E-011
LCX   10 1  1.78E-010
RSUB 30 5 0.0182719
RSUBNPN 55 5 213.779
Rpsinker 5 33 0.276699
*
RE1 35 3 1E-03
RE2 35 4 1E-03
*
Q1 11 22 33 55 M_BFP740F
*
.MODEL 	M_BFP740F	NPN(
+	TNOM = 25
+	IS	=	1.249E-015
+	BF	=	987.1
+	NF	=	1.002
+	VAF	=	380.1
+	IKF	=	0.1898
+	ISE	=	3.488E-015
+	NE	=	1.709
+	BR	=	115
+	NR	=	1.01
+	VAR	=	1.229
+	IKR	=	0.02753
+	ISC	=	3.205E-015
+	NC	=	2
+	RB	=	7.015
+	IRB	=	6.026E-005
+	RBM	=	1.974
+	RE	=	0.18
+	RC	=	4.121
+	XTB	=	-2.7
+	EG	=	1.09
+	XTI	=	0.1
+	CJE	=	2.531E-013
+	VJE	=	0.9286
+	MJE	=	0.06125
+	TF	=	2.331E-012
+	XTF	=	1.159
+	VTF	=	0.5242
+	ITF	=	0.3991
+	PTF	=	0.1
+	CJC	=	5.452E-014
+	VJC	=	0.4808
+	MJC	=	0.5812
+	XCJC	=	1
+	TR	=	1.532E-009
+	CJS	=	1.585E-013
+	MJS	=	0.2454
+   VJS =   1
+	FC	=	0.2329
+	KF	=	1.616E-021
+	AF	=	1.677)
***************************************************************
*
.ENDS BFP740F
