***************************************************************
* Comments for the user:
* The VBIC Model includes the self heating effect and the user can switch selfheating on or off.
* a) no self heating: set Rth = 0
* b) with self heating: set Rth=20K/W + Rth_PCB (customer)
* Note: Rth = 20 K/W includes only the thermal resistance of die and package and
* the thermal resistance of the customer PCB must be added (Rth=20K/W + Rth_PCB)

*	Infineon	Technologies	AG
*	VBIC	MODEL
*	VALID	UP	TO	6	GHZ
*	>>>	BFQ790	<<<
*	(C)	2015	Infineon	Technologies	AG
*	Version	1.0	Juni	2015
***************************************************************
*.OPTION TNOM=25, GMIN= 1.00e-12
*BFQ790 C B E

* without access to the external thermal node (Rth to be adjusted by the customer in the model parameter list for the transistor
.SUBCKT BFQ790 1 2 3
*
* with access to external thermal node
*.SUBCKT BFQ790 1 2 3 55
*
CCSPAR 11 44 1.375E-012
LEx    30 3 4.36882E-011
LBx    20 2 2.77681E-011
LCx    10 1 4.265E-011
CBEPCK 20 30  1.105E-013
CBCPCK 10 20  1E-014
CCEPCK 10 30  1E-015
LB   222 20 1.02449E-009
LC   10 11  1.89901E-009
*
RBL 222 22 45
CBL 222 22  3E-011
*
Rsub1 44 30 0.000500184
Rps 44 33 0.102733
*
*
* without access to the external thermal node
Q1 11 22 33 44 M_BFQ790
*
* with access to the thermal node
* Q1 11 22 33 44 55 M_BFQ790
*
.MODEL 	M_BFQ790 NPN Level=4(
+ Tnom=25
+ Cbeo=9.31E-012
+ Cje=7.693E-013
+ Pe=0.5892
+ Me=0.3115
+ Aje=-0.5
+ Wbe=1
+ Cbco=1.966E-013
+ Cjc=1E-015
+ Pc=0.5095
+ Mc=0.2797
+ Ajc=-0.5
+ Cjep=1.875E-012
+ Cjcp=2.067E-012
+ Ps=0.5086
+ Ms=0.2865
+ Ajs=-0.5
+ Fc=0.93
+ Vef=615.1
+ Ver=5.61838
+ Is=9.704E-015
+ Nf=1.001
+ Ibei=5E-017
+ Nei=1.013
+ Iben=3.609E-014
+ Nen=1.46
+ Ikf=2
+ Nr=0.9958
+ Ibci=1.001E-017
+ Nci=1.015
+ Ibcn=2.918E-014
+ Ncn=1.399
+ Ikr=0.2301
+ Wsp=1
+ Isp=1E-015
+ Nfp=1
+ Ibcip=2E-017
+ Ncip=1
+ Ibcnp=1.7E-015
+ Ncnp=1.67
+ Ikp=0.0002
+ Ibeip=7E-017
+ Ibenp=2.4E-014
+ Re=0.2
+ Rcx=0.02559
+ Rci=1.168
+ Qco=4E-015
+ Vo=0.4234
+ Gamm=2.199E-012
+ Hrcf=0.1907
+ Rbx=0.2825
+ Rbi=1.868
+ Rbp=0.001
+ Rs=10
+ Avc1=1
+ Avc2=25.84
+ Tf=3E-012
+ Qtf=0.6758
+ Xtf=0.01293
+ Vtf=0.5
+ Itf=0.1948
+ Tr=1E-015
+ Td=1E-015
+ Cth=0
+ Rth=20.05
+ Ea=1.12
+ Eaie=1.12
+ Eaic=1.12
+ Eais=1.12
+ Eane=1.12
+ Eanc=1.12
+ Eans=1.12
+ Xre=0
+ Xrb=0
+ Xrc=0
+ Xrs=0
+ Xvo=0
+ Xis=1.907
+ Xii=4.963
+ Xin=-2.837
+ Tnf=1E-006
+ Tavc=0.006705
+ Kfn=0
+ Afn=1
+ Bfn=1 )
***************************************************************
*
.ENDS BFQ790
