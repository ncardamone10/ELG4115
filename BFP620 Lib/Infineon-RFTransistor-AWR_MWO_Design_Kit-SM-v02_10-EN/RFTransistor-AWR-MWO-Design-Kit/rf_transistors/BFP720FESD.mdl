***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	10	GHZ
*	>>>	BFP720FESD	<<<
*	(C)	2012	Infineon	Technologies	AG
*	Version 3.1	JULY	2012
***************************************************************
* - Please use the global SPICE GP parameter TEMP to specify the
*	junction temperature of the device in your application to get
*	correct simulation results. This procedure is necessary because
*	the GP model does not consider the self heating of the device.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated DC power Pdc.
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 405 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature during measurement
*   for the parameter extraction. Please do not change this value.
****************************************************************
*.OPTION TNOM=25, GMIN= 1.00e-12
*BFP720FESD C B E1 E2

.SUBCKT BFP720FESD 1 2 3 4
*
*
CBEPAR 22 33 1.873E-013
CBCPAR 22 11 4.3E-014
CCEPAR 11 33 2.059E-013
LB   22 20 4.14E-010
LE   33 30 4.8E-011
LC   11 10  4.188E-010
CBEPCK 20 30  5E-014
CBCPCK 20 10  2E-015
CCEPCK 10 30  5E-014
LBX    20 2 177E-12
LEX   30 34 25.00E-12
LCX   10 1  177E-12
*
RE1 34 3 1E-03
RE2 34 4 1E-03
*
D1 33 25 M_D1
D2 55 25  M_D2
RBLfdb 22 25 3.2
RPS 33 55 0.4524
RSUB 30 55 800
D3 55 15 M_D3
D4 23 33 M_D4
D5 23 15 M_D5
RLDNBL 15 11 14.8
*
*
Q1 11 22 33 55 M_BFP720FESD
*
*
.MODEL M_D1 D(
+ IS=3.5E-015
+ N=1.02
+ RS=6.1
+ CJO=4E-014)
*
*
.MODEL M_D2 D(
+ IS=3.5E-015
+ N=1.02
+ RS=4170
+ CJO=4.5E-014)
*
*
.MODEL M_D3 D(
+ IS=7.835E-016
+ N=1.053
+ RS=1380
+ CJO =9.5E-014)
*
*
.MODEL M_D4 D(
+ IS=3.5E-015
+ N=1.02
+ RS=0.2
+ CJO =3E-014)
*
.MODEL M_D5 D(
+ IS=3.5E-015
+ N=1.02
+ RS=4.7
+ CJO =3E-014)
*
*
.MODEL 	M_BFP720FESD	NPN(
+	TNOM=	25
+	IS	=	7.621E-016
+	BF	=	719.4
+	NF	=	1.017
+	VAF	=	120.2
+	IKF	=	0.08618
+	ISE	=	2.328E-014
+	NE	=	2
+	BR	=	307.1
+	NR	=	1.012
+	VAR	=	1.455
+	IKR	=	0.008585
+	ISC	=	2.278E-014
+	NC	=	1.5
+	RB	=	7.151
+	IRB	=	0
+	RBM	=	1.031
+	RE	=	0.321
+	RC	=	5.9
+	XTB	=	-1.7
+	EG	=	1.11
+	XTI	=	0.1
+	CJE	=	6.754E-014
+	VJE	=	1.064
+	MJE	=	0.1384
+	TF	=	2.22714E-012
+	XTF	=	3.2
+	VTF	=	2.294
+	ITF	=	0.7341
+	PTF	=	0.1
+	CJC	=	4.36E-014
+	VJC	=	1.073
+	MJC	=	0.9219
+	XCJC	=	0.5
+	TR	=	2.035E-009
+	CJS	=	7.013E-014
+	MJS	=	0.09104
+   VJS =   1.063
+	FC	=	0.5
+	KF	=	3.4E-012
+	AF	=	1.5)
***************************************************************
*
*
.ENDS BFP720FESD
