***************************************************************
*	Infineon	Technologies	AG
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX
*	VALID	UP	TO	12	GHZ
*	>>>	BFR843EL3	<<<
*	(C)	2013	Infineon	Technologies	AG
*	Version 1.1	MAR	2013
***************************************************************
* - Please use the global SPICE GP parameter TEMP to specify the
*	junction temperature of the device in your application to get
*	correct simulation results. This procedure is necessary because
*	the GP model does not consider the self heating of the device.
* - TEMP is calculated by TEMP=TA+Pdc*(RthJS+RthSA). The junction
*   temperature TEMP is the sum of the ambient temperature TA and
*   the increment of temperature caused by the dissipated DC power Pdc.
* - RthJS is the thermal resistance between the junction and the
*   soldering point. RthJS for this device is 375 K/W. RthSA is the
*   thermal resistance of the PCB, from the soldering point to the
*   ambient. For determination of RthSA please refer to Infineon's
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature during measurement
*   for the parameter extraction. Please do not change this value.
****************************************************************
*.OPTION TNOM=25, GMIN= 1.00e-12
*BFR843EL3 C B E
.SUBCKT BFR843EL3 1 2 3
*
*
CBEPAR 22 33 2.608E-013
CBCPAR 22 11 4.054E-014
CCEPAR 11 33 3.59E-013
*
Cfeed 11 66 5.2E-012
Rfeed 66 22 1000
Cfeedpar 66 33 1.672E-013
*
LB    22 20 3.265E-010
LC   11 10  3.426E-010
CBEPCK 20 30  1E-014
CBCPCK 20 10  1.5E-014
CCEPCK 10 30  1E-014
LBX    20 2 6E-011
LEX   30 3 1.5E-011
LCX   10 1  6E-011
*
K_BC  LB LC 0.05034
*
R_CS_npn 55 5 500
*
D1 33 25 M_D1
D2 5 25  M_D2
*
R_NBL_fdb 22 25 3.2
R_PS 33 5 0.2131
RSUB 30 5 0.0246
*
D3 5 15 M_D3
D4 23 33 M_D4
D5 23 15 M_D5
*
R_NBL_e11g 15 11 1.8
*
Q1 11 22 33 55 M_BFR843EL3
*
.MODEL M_D1 D(
+ IS=3E-015
+ N=1
+ RS=2.846
+ CJO=4E-014)
*
.MODEL M_D2 D(
+ IS=3E-015
+ N=1
+ RS=4700
+ CJO=4.5E-014)
*
.MODEL M_D3 D(
+ IS=6.911E-016
+ N=1.1
+ RS=1380
+ CJO =9.5E-014)
*
.MODEL M_D4 D(
+ IS=3.5E-015
+ N=1
+ RS=0.2
+ CJO =3E-014)
*
.MODEL M_D5 D(
+ IS=3.5E-015
+ N=1.02
+ RS=4.7
+ CJO =3E-014)
*
.MODEL 	M_BFR843EL3	NPN(
+	TNOM = 25
+	IS	=	1.889E-016
+	BF	=	784.7
+	NF	=	1
+	VAF	=	375.1
+	IKF	=	0.1713
+	ISE	=	2.454E-015
+	NE	=	1.908
+	BR	=	98.98
+	NR	=	0.9941
+	VAR	=	1.375
+	IKR	=	0.05974
+	ISC	=	1.578E-015
+	NC	=	1.8
+	RB	=	5.716
+	IRB	=	0
+	RBM	=	1.477
+	RE	=	0.4167
+	RC	=	7.24
+	XTB	=	-2.276
+	EG	=	1.11
+	XTI	=	-1.233
+	CJE	=	8.091E-014
+	VJE	=	0.9141
+	MJE	=	0.06778
+	TF	=	1.007E-012
+	XTF	=	1
+	VTF	=	0.1832
+	ITF	=	0.5237
+	PTF	=	0.5
+	CJC	=	3.125E-014
+	VJC	=	0.6892
+	MJC	=	0.5349
+	XCJC	=	0.8
+	TR	=	1E-009
+	CJS	=	2.575E-013
+	MJS	=	0.5599
+   VJS =   0.4343
+	FC	=	0.5715
+	KF	=	2.476E-016
+	AF	=	1.517)
***************************************************************
*
.ENDS BFR843EL3
